.program cust_nec_control

; Transmits the proprietary IR Air Conditioner frame.
;
; Accepts 4 x 32-bit words (note: first one is a special header) from the
; TX FIFO and sends them least-significant bit first using Pulse
; Distance Modulation.
;
.define BURST_IRQ         7             ; the IRQ used to trigger a carrier burst
.define SYNC_BURSTS       11            ; how many bursts to transmit for a SYNC
.define SYNC_SPACE_DELAY  60            ; start delay (in ticks) after sync
.define SHORT_DELAY       8             ; short delay (space) for logical 0
.define LONG_DELAY        30            ; long delay (space) for logical 1

.wrap_target
    pull                                ; fetch the header word

    set X, (SYNC_BURSTS - 1)            ; send a sync burst
sync_burst:
    irq wait BURST_IRQ [9]
    jmp X-- sync_burst

    ; first 4LSb: the number of words to be sent
    out Y, 4 [SYNC_SPACE_DELAY / 2 - 2]
    ; next 4LSb: initial header shift / ignore bytes
    out X, 4 [SYNC_SPACE_DELAY / 2 - 2]
    jmp header_check
header_shift:
    ; shift / ignore X bytes from the OSR
    out NULL, 8
header_check:
    jmp X-- header_shift
    jmp data_bit_start

data_pull:
    pull
data_bit:
    out X, 1
    jmp !X data_bit_start
    nop [LONG_DELAY]
data_bit_start:
    irq wait BURST_IRQ [10]
    irq wait BURST_IRQ [10 + SHORT_DELAY]

    jmp !OSRE data_bit
    jmp Y-- data_pull
.wrap


% c-sdk {
static inline void cust_nec_control_program_init (PIO pio, uint sm, uint offset, float freq, int bits_per_frame)
{
    pio_sm_config c = cust_nec_control_program_get_default_config(offset);

    sm_config_set_out_shift (&c,
			true,       // shift right
			false,      // disable autopull
			bits_per_frame);

    // join the FIFOs to make a single large transmit FIFO
    sm_config_set_fifo_join (&c, PIO_FIFO_JOIN_TX);

    // configure the clock divider
    float div = clock_get_hz (clk_sys) / (freq);
    sm_config_set_clkdiv (&c, div);

    // apply the configuration to the state machine
    pio_sm_init(pio, sm, offset, &c);
}
%}
